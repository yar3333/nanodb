package generator;

import orm.Db;
import php.Global;
import php.Syntax;
import php.TypedArray;
using php.StringToolsNative;

class OrmGenerator
{
	var outPath : String;
	
	public function new(outPath:String)
    {
		this.outPath = outPath != "" ? outPath.replace("\\", "/").rtrim("\\/") + "/" : "";
	}
	
	public function generate(db:Db, autogenPackage:String, customPackage:String, ignoreTables:TypedArray<String>, noInstantiateManagers:TypedArray<String>, positions:OrmPositions)
	{
		var autogenOrmClassName = autogenPackage + ".Orm";
		var customOrmClassName = customPackage + ".Orm";
		
		var tables = new TypedArray<OrmTable>();
		
		Syntax.foreach(db.connection.getTables(), function(_, tableName:String)
        {
			if (!ignoreTables.hasValue(tableName))
			{
				var table = new OrmTable(tableName, autogenPackage, customPackage);
				new OrmModelGenerator().make(db, table, customOrmClassName, outPath, positions);
				new OrmManagerGenerator().make(db, table, customOrmClassName, outPath, positions);
				new OrmQueryGenerator().make(db, table, outPath, positions);
				tables.push(table);
			}
        });
		
		Log.start("MANAGERS => " + customOrmClassName);
		makeAutogenOrm(tables, autogenOrmClassName, customOrmClassName, noInstantiateManagers);
		makeCustomOrm(customOrmClassName, autogenOrmClassName);
		Log.finishSuccess();
    }
	
	
	function makeAutogenOrm(tables:TypedArray<OrmTable>, autogenOrmClassName:String, customOrmClassName:String, noInstantiateManagers:TypedArray<String>)
	{
		var autogenOrm = getAutogenOrm(tables, autogenOrmClassName, noInstantiateManagers);
		var destFileName = outPath + autogenOrmClassName.replace(".", "/") + ".php";
		Tools.mkdir(Global.dirname(destFileName));
		Global.file_put_contents(
			 destFileName
			,"<?php\n// This file is autogenerated. Do not edit!\n\n" + autogenOrm.toString()
		);
	}
	
	function makeCustomOrm(customOrmClassName:String, autogenOrmClassName:String)
	{
		if (!Global.file_exists(outPath + "/" + customOrmClassName.replace(".", "/") + ".php"))
		{
			var customOrm = getCustomOrm(customOrmClassName, autogenOrmClassName);
			var destFileName = outPath + customOrmClassName.replace(".", "/") + ".php";
			Tools.mkdir(Global.dirname(destFileName));
			Global.file_put_contents(destFileName,"<?php\n\n" + customOrm.toString());
		}
	}
	
	function getAutogenOrm(tables:TypedArray<OrmTable>, fullClassName:String, noInstantiateManagers:TypedArray<String>) : PhpClass
	{
		var clas = new PhpClass(fullClassName);
		
		Syntax.foreach(tables, function(_, t:OrmTable)
		{
			clas.addVar(new PhpVar(t.varName, Tools.toPhpType(t.customManagerClassName)));
		});
		
		clas.addMethod(
			  "__construct"
			, Syntax.arrayDecl(new PhpVar("db", Tools.toPhpType("nanodb.orm.Db")))
			, null
			, tables
				.filter(function(t) return !noInstantiateManagers.hasValue(t.tableName))
				.map(function(t) return "$this->" + t.varName + " = new " + Tools.toPhpType(t.customManagerClassName) + "($db, $this);")
				.join("\n")
		);
        
		return clas;
	}
	
	function getCustomOrm(fullClassName:String, autogenOrmClassName:String) : PhpClass
	{
		return new PhpClass(fullClassName, autogenOrmClassName);
	}
}
